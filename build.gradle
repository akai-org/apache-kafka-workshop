plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.protobuf' version "0.9.5"
}

group = 'pl.org.akai'
version = '1.0.0'

def protobufVersion = '3.25.5'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        url = "https://packages.confluent.io/maven"
    }
}

dependencies {
    // Spring WebFlux
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Reactive H2 Database support
    implementation 'io.r2dbc:r2dbc-h2'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'

    // Kafka
    implementation 'org.springframework.kafka:spring-kafka'

    // Protobuf
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation 'io.confluent:kafka-protobuf-serializer:7.9.0'

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins { java {} }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
